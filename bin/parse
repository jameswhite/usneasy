#!/usr/bin/env perl
use Mail::Internet;
use Data::Dumper;
use Time::Piece;
use FileHandle;
use JSON;
use strict;

package USNEasy;
use strict;
# use warnings;

sub new{
  my $class = shift;
  my $usn = shift;
  my $self = {};
  $self->{'debug'} = 0;
  $self->{'notice'} = $usn;
  $self->{'begins_with'} = {
                             'header'              => '^=+$',
                             'affects'             => '^(A security issue affects these releases of Ubuntu and its derivatives|A security issue affects the following Ubuntu releases):',
                             'summary'             => '^(Summary):',
                             'description'         => '^(Software Description):',
                             'details'             => '^(Details|Details (F|f)ollow):',
                             'update_instructions' => '^(Update (I|i)nstructions):',
                             'references'          => '^(References):',
                             'package_information' => '^(Package Information):',
                             'next_part'           => '^-+ next part -+$',
                           };
  bless $self, $class;
  return $self;
}

sub any_section_header{
  my $self = shift;
  my @regexes;
  foreach my $key (sort(keys(%{$self->{'begins_with'}}))){
    push(@regexes,$self->{'begins_with'}->{$key});
  }
  return "(".join('|',@regexes).")";
}

sub notice{
  my $self = shift;
  $self->{'notice'} = shift if @_;
  return $self->{'notice'};
}

# A section will be defined as the section header, until the start of any next section.
sub section{
  my $self = shift;
  my $type = shift;
  return $self->{$type} if defined($self->{$type});
  return undef unless(defined($self->{'begins_with'}->{$type}));
  my $in_section=0;
  my $has_section=0;
  my $section=[];
  my $stop_regex = $self->any_section_header;
  foreach my $line (split/\n/,$self->notice){
      if($line=~m/$self->{'begins_with'}->{$type}/){
         if ($in_section==0){
             $has_section=1;
             $in_section = 1;
         }else{
             $in_section = 0;
         }
      }elsif($line=~m/$stop_regex/){
        $in_section = 0 if ($in_section==1);
      }
      if($in_section == 1){
        if($in_section == 1){
           push(@{$section},$line) unless( ($type == 'header') && ($line=~m/$self->{'begins_with'}->{$type}/) );
        }
        if($self->{'debug'} > 0){
          if( ($type == 'header') && ($line=~m/$self->{'begins_with'}->{$type}/) ){
            print ":OUT: ".$line."\n";
          }else{
            print ": IN: ".$line."\n";
          }
        }
      }else{
        print ":OUT: ".$line."\n" if $self->{'debug'} > 0;
      }

  }
  return undef unless $has_section;
  $self->{$type} = join("\n",@{$section});
  return $self->{$type};
}

sub header{              my $self=shift; return $self->section('header');              }
sub affects{             my $self=shift; return $self->section('affects');             }
sub summary{             my $self=shift; return $self->section('summary');             }
sub description{         my $self=shift; return $self->section('description');         }
sub details{             my $self=shift; return $self->section('details');             }
sub update_instructions{ my $self=shift; return $self->section('update_instructions'); }
sub references{          my $self=shift; return $self->section('references');          }
sub package_information{ my $self=shift; return $self->section('package_information'); }
sub next_part{           my $self=shift; return $self->section('next_part');           }

sub has_header{              my $self=shift; return 1 if defined($self->header);              return 0; }
sub has_affects{             my $self=shift; return 1 if defined($self->affects);             return 0; }
sub has_summary{             my $self=shift; return 1 if defined($self->summary);             return 0; }
sub has_description{         my $self=shift; return 1 if defined($self->description);         return 0; }
sub has_details{             my $self=shift; return 1 if defined($self->details);             return 0; }
sub has_update_instructions{ my $self=shift; return 1 if defined($self->update_instructions); return 0; }
sub has_references{          my $self=shift; return 1 if defined($self->references);          return 0; }
sub has_package_information{ my $self=shift; return 1 if defined($self->package_information); return 0; }
sub has_next_part{           my $self=shift; return 1 if defined($self->next_part);           return 0; }

sub header_info{
  my $self=shift;
  return undef unless $self->has_header;
  my $header_line = join(' ',split(/\n/,$self->header));
  if($header_line=~m/((January|February|March|April|May|June|July|August|September|October|November|December) ([0-9]{1,2}), ([0-9]{4}))/){
    $self->{'date'}  = $1;
    $self->{'month'} = $2;
    $self->{'day'}   = $3;
    $self->{'year'}  = $4;
  }
  $header_line=~s/$self->{'date'}//;
  if($header_line=~m/(Ubuntu Security Notice ([^\s]+))/){
    $self->{'usn_text'}  = $1;
    $self->{'usn'}  = $2;

  }
  $header_line=~s/$self->{'usn_text'}//;
  $header_line=~s/^\s+//;
  $header_line=~s/\s+$//;
  $self->{'tagline'}=$header_line;
  return $self;
}

sub header_date{
  my $self = shift;
  return $self->{'date'} if defined $self->{'date'};
  $self->header_info;
  return $self->{'date'};
}

sub header_usn{
  my $self = shift;
  return $self->{'usn'} if defined $self->{'usn'};
  $self->header_info;
  return $self->{'usn'};
}

sub header_tagline{
  my $self = shift;
  return $self->{'tagline'} if defined $self->{'tagline'};
  $self->header_info;
  return $self->{'tagline'};
}

sub instructions{
  my $self=shift;
  return undef unless $self->has_update_instructions;
  my @instructions = split(/\n/,$self->update_instructions);
  my $in_packages=0;
  my $packages={};
  my @special_instructions;
  my $distro;
  while($#instructions > -1){
    my $line = shift(@instructions);
    if($in_packages == 1){
        if($line=~m/^((Ubuntu)\s+([^:]*):*)/){
          $distro = $3;
          $distro=~s/ LTS$//;
        }elsif($line=~m/^  (\S+)\s+(\S+)/){
          push(@{ $packages->{$distro} }, {'package' => $1, 'version' => $2});
        }elsif($line=~/^\s*$/){
          next;
        }else{
           $in_packages = 0;
           push(@special_instructions,$line) unless($line=~m/^\s*$/);
        }
     }else{
        if($line =~m/The problem can be corrected by updating your system to the following/){
          $line = $line." ".shift(@instructions);
          if($line =~m/The problem can be corrected by updating your system to the following package versions:/){
            $in_packages=1;
          }
        }
        push(@special_instructions,$line) unless($line=~m/^\s*$/);
     }
  }
  $self->{'package_updates'}=$packages;
  $self->{'special_instructions'}=join("\n",@special_instructions);
}

sub package_updates{
  my $self=shift;
  $self->instructions unless( defined($self->{'package_updates'}) );
  return $self->{'package_updates'};
}

sub special_instructions{
  my $self=shift;
  $self->instructions unless( defined($self->{'special_instructions'}) );
  return $self->{'special_instructions'};
}

sub cves{
  my $self = shift;
  my $references = $self->references;
  @{$self->{'cves'}} = $references=~/([Cc][Vv][Ee]-[0-9]+-[0-9]+)/g;
  return $self->{'cves'};
}

sub detail_breakdown{
  my $self = shift;
  $self->{'details'} = $self->details unless defined($self->{'details'});
  my @lines = split(/\n/,$self->{'details'});
  my $paragraphs = [];
  my $paraglines = [];
  foreach my $line (@lines){
    if($line=~m/^\s*$/g){
      if($#{ $paraglines } > -1){
        push(@{ $paragraphs },$paraglines);
        $paraglines = [];
      }
    }else{
      push(@{ $paraglines }, $line);
    }
  }
  if($#{ $paraglines } > -1){
    push(@{ $paragraphs },$paraglines);
  }
  $self->{'detail_paragraphs'} = $paragraphs;
  return $self;
}

sub detail_paragraphs{
  my $self = shift;
  $self->detail_breakdown unless(defined($self->{'detail_paragraphs'}));
  return $self->{'detail_paragraphs'};
}

sub detail_sentences{
  my $self = shift;
  return $self->{'detail_sentences'} if(defined($self->{'detail_sentences'}));
  $self->detail_breakdown unless(defined($self->{'detail_paragraphs'}));
  foreach my $paragraph (@{ $self->{'detail_paragraphs'} }){
    my $pgraphoneline = join(' ',@{$paragraph});
    my @sentences = split(/\. /,$pgraphoneline);
    push(@{ $self->{'detail_sentences'} }, @sentences);
  }
  return $self->{'detail_sentences'};
}

sub attacker_capability{
  my $self = shift;
  return $self->{'attacker_capability'} if(defined($self->{'attacker_capability'}));
  $self->detail_sentences unless(defined($self->{'detail_sentences'}));
  foreach my $sentence (@{ $self->{'detail_sentences'} }){
    push(@{ $self->{'attacker_capability'} }, "$sentence.") if($sentence =~m/attacker/);
  }
  return $self->{'attacker_capability'};
}
1;

################################################################################

my $directory = $ARGV[0];
opendir(my $dh, $directory);
my @mboxes = sort_mboxes(grep { /.txt$/ && -f "$directory/$_" } readdir($dh));
closedir $dh;
# print Data::Dumper->Dump([@mboxes]);
my $mails = [];
my $current_mail = [];

foreach my $mbox (@mboxes){
    my $fh = FileHandle->new("${directory}/$mbox", "r");
        if (defined $fh) {
            while( my $line=<$fh> ){
              if($line=~m/^From /){
                push(@{$mails},Mail::Internet->new( $current_mail )) if $#{$current_mail} > 0;
                $current_mail = [];
              }
              push(@{$current_mail}, $line);
            }
            push(@{$mails},Mail::Internet->new( $current_mail )) if $#{$current_mail} > 0;
            undef $fh;       # automatically closes the file
        }
}

foreach my $mail (@{$mails}){
    my $notice = USNEasy->new(join('',@{$mail->body}));
    next unless $notice->has_header;
    next unless $notice->update_instructions;
    my $dump=1;
    if($dump){
      my $json = JSON->new->allow_nonref;
      print $json->encode({
                            'date'    => $notice->header_date,
                            'usn'     => $notice->header_usn,
                            'info'    => $notice->header_tagline,
                            'update'  => $notice->package_updates,
                            'special' => $notice->special_instructions,
                            'CVEs'    => $notice->cves,
                            'threat'  => $notice->attacker_capability,
                          });
     }else{
       print Data::Dumper->Dump([$notice->attacker_capability]);
     }
}
exit 0;

################################################################################

sub sort_mboxes(){
    my @mailboxes = @_;
    my $idx = { 'January'   => '01', 'February'  => '02', 'March'     => '03',
                'April'     => '04', 'May'       => '05', 'June'      => '06',
                'July'      => '07', 'August'    => '08', 'September' => '09',
                'October'   => '10', 'November'  => '11', 'December'  => '12' };
    my $ridx = {};
    foreach my $i (keys(%{$idx})){ $ridx->{ $idx->{$i} } = $i; }
    my @numerical_mboxes;
    foreach my $mbox (@mailboxes){
      $mbox=~s/.txt//g;
      if($mbox=~m/-(.*)/){ my $month=$1 ; $mbox=~s/-$month/$idx->{$month}/; }
      push(@numerical_mboxes,$mbox);
    }
    my @result;
    foreach my $mbox ( sort @numerical_mboxes ){
      if($mbox=~m/([0-9]{4})([0-9]{2})/){ push(@result, "$1-$ridx->{$2}.txt"); }
    }
    return @result;
}
